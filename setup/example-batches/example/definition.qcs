#
# An example QC-Script
#

meta title "QualityCrowd Example Batch"
meta description "This Batch demonstrates all implemented features."

meta workers 30
meta timeout 1200

var sixpoints "1: Bad; 2: Poor; 3: Fair; 4: Good; 5: Excellent; 6: Wow!"

#set mediaurl "http://.../media/"
set videowidth 352
set videoheight 288
set skipvalidation

#
# Macro and list definitions
#

macro qualityquestion
  set answermode continous
  set width 750
  set answers $sixpoints
  question "Please rate the visual video quality"
end macro

list myvideos
	foreman_plr10.mp4
    foreman_plr5.mp4
    foreman_plr0.4.mp4
end list

#
# The Steps
# 

step "Welcome page"
	title "Welcome"
	text include(page.welcome.html)
end step

step "First question"
	set answermode text
	title "Let's begin"
	text "with an easy question:"
	question "Where are you from?"
end step

step "Animal and color question"
	set answermode strings
	set answers "animal: What is your favorite animal?; color: What is your favorite color?"
	title ""
	text "Here comes another question for starters:"
	question "What do you like most?"
end step

step "Qualification"
	qualification example-qualification
end step

set answermode discrete
set answers $sixpoints
set delay 3

step "delayed video quality"
	title "Image quality"
	image lena.png
	question "Please rate the ""visual"" image quality"
end step

set delay 0

step "Stupid question with elephant"
	set answers "1: A woman; 2: A man; 3: Two women; 4: An elephant"
	question "What did you see in the last image?"
end step

for video in myvideos
  step $video
      video $video
      $qualityquestion
  end step
end for

step 
	title "Done"
	text "You have finished the task!"
end step
